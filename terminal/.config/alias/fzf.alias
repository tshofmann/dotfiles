# ============================================================
# fzf.alias - Generische fzf-Funktionen
# ============================================================
# Zweck       : Tool-unspezifische fzf-Utilities
# Pfad        : ~/.config/alias/fzf.alias
# Docs        : https://github.com/junegunn/fzf
# Config      : ~/.config/fzf/config
# Nutzt       : bat (Code), eza (Verzeichnis), fd (Default-Command), tldr (help)
# Nutzt       : pdftotext (PDF), 7zz (Archive), identify (Bilder), ffprobe (Video/Audio)
# ============================================================
# Tool-spezifische Funktionen in fd/rg/git/brew/gh.alias.
#
# Helper-Skripte in ~/.config/fzf/:
#   config    – Globale fzf-Optionen (Farben, Layout, Keybindings)
#   init.zsh  – Shell-Integration (Ctrl+X Keybindings, FZF_DEFAULT_COMMAND)
#   lib.zsh   – Shared Utilities (Farben, ANSI-Strip)
#   preview   – Vorschau (file|dir) mit bat/eza
#   action    – Sichere Aktionen (copy, edit, open, git-*, zoxide-*)
#   help      – Helper für help() (list|preview|toggle)
#   procs     – Helper für procs() (list)
#   cmds      – Helper für cmds() (preview)
# Aliase      : procs, help, cmds, vars
# ============================================================

# Guard   : Nur wenn fzf installiert ist
if ! command -v fzf >/dev/null 2>&1; then
    return 0
fi

# ------------------------------------------------------------
# Shell-Keybindings (in init.zsh konfiguriert, Ctrl+X Prefix)
# ------------------------------------------------------------
# Ctrl+X 1: History durchsuchen mit Vorschau
# Ctrl+X 2: Datei suchen mit bat-Vorschau, Pfad einfügen
# Ctrl+X 3: Verzeichnis wechseln mit eza-Vorschau

# ------------------------------------------------------------
# KILL + FZF: Prozess-Management
# ------------------------------------------------------------
# Prozesse interaktiv suchen und beenden (Tab für Mehrfachauswahl)
# Prozess Browser(signal=15) – Enter=Beenden, Tab=Mehrfach, Ctrl+S=Apps↔Alle
procs() {
    local pid
    local helper="${XDG_CONFIG_HOME:-$HOME/.config}/fzf"

    pid=$("$helper/procs" list apps | \
        fzf -m \
            --freeze-left=1 \
            --prompt='Apps> ' \
            --header='Enter: Beenden | Tab: Mehrfach | Ctrl+S: Apps/Alle' \
            --bind "ctrl-s:transform:[[ \$FZF_PROMPT == 'Apps> ' ]] && echo 'reload($helper/procs list all)+change-prompt(Alle> )' || echo 'reload($helper/procs list apps)+change-prompt(Apps> )'" | \
        awk '{print $1}')

    [[ -n "$pid" ]] && echo "$pid" | xargs kill -${1:-15}
}

# ------------------------------------------------------------
# MAN + FZF + BAT: Man-Pages Browser
# ------------------------------------------------------------
# Man-Pages interaktiv durchsuchen mit Syntax-Highlighting
# Man/tldr Browser – Ctrl+S=Modus wechseln (Liste + Preview), Enter=öffnen
help() {
    local output mode selection page section
    local fzf_dir="${XDG_CONFIG_HOME:-$HOME/.config}/fzf"

    # Ctrl+S wechselt sowohl Liste als auch Preview zwischen man und tldr
    output=$("$fzf_dir/help" list man | \
        fzf --prompt='man> ' \
            --preview="$fzf_dir/help preview man {}" \
            --header='Enter: öffnen | Ctrl+S: man ↔ tldr' \
            --bind="ctrl-s:transform:$fzf_dir/help toggle" \
            --bind="enter:transform:[[ \$FZF_PROMPT == 'tldr> ' ]] && echo 'print(tldr)+accept' || echo 'print(man)+accept'")

    [[ -z "$output" ]] && return 0

    mode=$(echo "$output" | head -1)
    selection=$(echo "$output" | tail -1)
    page=$(echo "$selection" | sed 's/(.*//' | xargs)

    if [[ "$mode" == "tldr" ]]; then
        if command -v tldr >/dev/null 2>&1; then
            tldr "$page"
        else
            echo "tldr nicht installiert. Fallback auf: man $page" >&2
            man "$page"
        fi
    else
        section=$(echo "$selection" | sed -n 's/.*([^0-9]*\([0-9][a-z]*\)).*/\1/p')
        if [[ -n "$section" ]]; then
            man "$section" "$page"
        else
            man "$page"
        fi
    fi
}

# ------------------------------------------------------------
# ALIAS + FZF: Alias-/Funktions-Browser
# ------------------------------------------------------------
# Fuzzy-Suche für Aliase und Funktionen aus allen .alias-Dateien
# Befehl Browser(suche?) – Enter=Übernehmen, Ctrl+S=tldr↔Code
cmds() {
    local fzf_dir="${XDG_CONFIG_HOME:-$HOME/.config}/fzf"
    local query="${1:-}"
    local selection name

    # Formatierte Ausgabe für fzf (Farben aus theme-style)
    selection=$("$fzf_dir/cmds" list | awk -F'|' \
        -v mauve="$C_MAUVE" -v green="$C_GREEN" -v blue="$C_BLUE" \
        -v text="$C_TEXT" -v reset="$C_RESET" '
        NF >= 3 {
            name = $1
            typ = ($2 == "alias") ? "alias" : "func "
            cat = $3
            desc = $4
            typcolor = ($2 == "alias") ? green : blue
            printf "%s%-12s%s %s%-5s%s %s%-10s%s %s%s%s\n",
                mauve, name, reset,
                typcolor, typ, reset,
                text, cat, reset,
                text, desc, reset
        }
    ' | fzf --ansi --query="$query" \
        --prompt='tldr> ' \
        --header='Enter: Befehl übernehmen | Ctrl+S: tldr ↔ Code' \
        --preview "[[ \$FZF_PROMPT == 'code> ' ]] && zsh '$fzf_dir/cmds' preview code {} || zsh '$fzf_dir/cmds' preview tldr {}" \
        --bind "ctrl-s:transform:[[ \$FZF_PROMPT == 'tldr> ' ]] && echo 'change-prompt(code> )+refresh-preview' || echo 'change-prompt(tldr> )+refresh-preview'")

    # Bei Enter: Befehl ins Edit-Buffer (kann editiert/ausgeführt werden)
    if [[ -n "$selection" ]]; then
        name=$(echo "$selection" | sed 's/\x1b\[[0-9;]*m//g' | awk '{print $1}')
        print -z "$name"
    fi
}

# ------------------------------------------------------------
# ENV + FZF: Umgebungsvariablen-Browser
# ------------------------------------------------------------
# Umgebungsvariablen durchsuchen mit formatierter Anzeige
# Variablen Browser – Enter=Export→Edit, Ctrl+Y=Kopieren
vars() {
    local selection name value
    local helper="${XDG_CONFIG_HOME:-$HOME/.config}/fzf"

    # Farbkodiert KEY=VALUE-Zeilen nach Kategorie (Catppuccin Mocha)
    # Dotfiles/XDG=Mauve, PATH=Green, Tools=Blue, Rest=Text
    _fenv_colorize() {
        awk -F= -v mauve="$C_MAUVE" -v green="$C_GREEN" -v blue="$C_BLUE" \
            -v text="$C_TEXT" -v reset="$C_RESET" '{
            name = $1
            value = substr($0, length($1) + 2)
            gsub(/\n/, " ", value)

            # Farbauswahl nach Kategorie
            if (name ~ /^(DOTFILES|XDG|CONFIG)/) {
                color = mauve  # Dotfiles-Variablen
            } else if (name ~ /PATH|DIRS/) {
                color = green  # PATH-artige Variablen
            } else if (name ~ /^(FZF|BAT|EZA|RG|FD)/) {
                color = blue   # Tool-Variablen
            } else {
                color = text   # Andere
            }

            if (length(value) > 45) {
                value = substr(value, 1, 42) "..."
            }
            printf "%s%-28s%s │ %s%s%s\n", color, name, reset, text, value, reset
        }'
    }

    selection=$(printenv | grep -E '^[a-zA-Z_][a-zA-Z0-9_]*=' | sort | _fenv_colorize | fzf --ansi \
        --header='Enter: Export → Edit | Ctrl+Y: Wert kopieren' \
        --preview='n=$(echo {} | cut -d"│" -f1 | xargs | grep -E "^[a-zA-Z_][a-zA-Z0-9_]*$")
            [[ -z "$n" ]] && exit 0
            v=$(printenv "$n" 2>/dev/null)
            printf "%s%s%s\n\n" "$C_MAUVE" "$n" "$C_RESET"
            if [[ "$v" == *:* ]] && [[ "$n" == *PATH* || "$n" == *DIRS* ]]; then
                echo "$v" | tr ":" "\n" | nl -ba
            else
                echo "$v"
            fi' \
        --bind="ctrl-y:execute-silent(n=\$(echo {} | cut -d'│' -f1 | xargs | grep -E '^[a-zA-Z_][a-zA-Z0-9_]*\$'); [[ -n \"\$n\" ]] && $helper/action copy \"\$(printenv \"\$n\")\")+abort")

    if [[ -n "$selection" ]]; then
        name=$(echo "$selection" | cut -d'│' -f1 | xargs)
        value=$(printenv "$name")
        # Export-Statement ins Edit-Buffer (editierbar vor Ausführung)
        print -z "export ${name}=\"${value}\""
    fi
}
